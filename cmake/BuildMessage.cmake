#-------------------------------------------------------------------------------
# Copyright (c) 2020, Arm Limited. All rights reserved.
#
# SPDX-License-Identifier: BSD-3-Clause
#
#-------------------------------------------------------------------------------

#[===[.rst:
BuildMessage
============

This module builds upon ``stgt_link_build_messages()`` from the |TFACMF| and
serves as "project specific configuration". |TFACMF| captures how build message
generation does work, and this module captures what information the build
message file shall hold.

This module generates a source file called ``build_message.c``. The generated
file captures the version string and the time stamp of the build.

The version string is extracted from the git working copy, and ``__TIME__``
and ``__DATE__`` ANSI-C macros are used for the timestamp.

The generated file defines two global variables:

   - ``const char build_message[]`` is a string capturing the time stamp
   - ``const char version_string`` is a string capturing the version string

When included, the module will create a CMake object library named
``build_message``. Any executable target wishes to use the "build message" shall
call ``stgt_link_build_messages()`` and pass the name of the object library
(``build_message``) to it.

Example::

    include(${TFA_ROOT_DIR}/cmake/BuildMessage)
    ...
    add_executable(my_exe ...)
    ...
    stgt_link_build_messages(NAME my_exe LIBS build_message)
    ...

#]===]

if(NOT DEFINED TFA_ROOT_DIR)
	message(FATAL_ERROR "TF-A root dir not defined")
endif()

set(BUILD_MESSAGE_TIMESTAMP "__TIME__\", \"__DATE__")
set(VERSION_STRING "BUILD_STRING")

stgt_create(NAME build_message)
stgt_set_target(NAME build_message TYPE objlib ARGS EXCLUDE_FROM_ALL)

stgt_add_src(NAME build_message SRC
	${CMAKE_CURRENT_BINARY_DIR}/build_message.c
)

find_package(Git)

add_custom_command(OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/build_message.c
						COMMAND ${CMAKE_COMMAND} -E echo "/* This file is generated by the build system. Do not edit! */" > ${CMAKE_CURRENT_BINARY_DIR}/build_message.c
						COMMAND ${CMAKE_COMMAND} -E echo "#define STR(x) #x" >> ${CMAKE_CURRENT_BINARY_DIR}/build_message.c
						COMMAND ${CMAKE_COMMAND} -E echo "#define TOSTRING(x) STR(x)" >> ${CMAKE_CURRENT_BINARY_DIR}/build_message.c
						COMMAND ${CMAKE_COMMAND} -E echo_append "#define GIT_DESC " >> ${CMAKE_CURRENT_BINARY_DIR}/build_message.c
						COMMAND ${GIT_EXECUTABLE} -C ${TFA_ROOT_DIR} describe --always --dirty --tags >> ${CMAKE_CURRENT_BINARY_DIR}/build_message.c
						COMMAND ${CMAKE_COMMAND} -E echo "#define BUILD_STRING TOSTRING(GIT_DESC)" >> ${CMAKE_CURRENT_BINARY_DIR}/build_message.c
						COMMAND ${CMAKE_COMMAND} -E echo "const char build_message[] = \"Built : \"${BUILD_MESSAGE_TIMESTAMP}; const char version_string[] = ${VERSION_STRING};" >> ${CMAKE_CURRENT_BINARY_DIR}/build_message.c
						DEPENDS "$<TARGET_GENEX_EVAL:build_message,$<TARGET_PROPERTY:build_message,BUILD_MESSAGE_DEPENDS>>"
						VERBATIM)
